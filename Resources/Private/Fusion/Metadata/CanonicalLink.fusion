prototype(Neos.Seo:CanonicalLink) < prototype(Neos.Fusion:Tag) {
    tagName = 'link'
    attributes {
        rel = 'canonical'
        href = Neos.Fusion:Component {
            node = ${documentNode}
            canonicalLink = ${q(this.node).property('canonicalLink')}

            renderer = Neos.Fusion:Case {
                node {
                    condition = ${Neos.Link.hasSupportedScheme(props.canonicalLink)}
                    renderer = Neos.Seo:Helper.LiveUri {
                        node = ${Neos.Link.convertUriToObject(props.canonicalLink, props.node)}
                        absolute = true
                    }
                }

                external {
                    condition = ${!String.isBlank(props.canonicalLink)}
                    renderer = ${props.canonicalLink}
                }

                default {
                    condition = true
                    renderer = Neos.Seo:Helper.LiveUri {
                        node = ${props.node}
                        absolute = true
                    }
                }
            }
        }
    }
    @if.hideIfNoIndexIsSet = ${q(node).property('metaRobotsNoindex') || q(node).is('[instanceof Neos.Seo:NoindexMixin]') ? false : true}
}
